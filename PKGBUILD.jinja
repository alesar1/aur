# Maintainer: Gilbert Gilb's <gilbsgilbert@gmail.com>

pkgname=fleet-orbit
pkgver={{ metadata.tag_info.pkgver }}
pkgrel=1
pkgdesc='Eases the deployment of osquery connected with a Fleet server.'
arch=(x86_64)
url='https://github.com/fleetdm/fleet/tree/main/orbit'
license=(MIT)
depends=(osquery)
makedepends=(go)
backup=(etc/default/orbit)
optdepends=()
source=(
  "https://github.com/fleetdm/fleet/archive/refs/tags/orbit-v$pkgver.tar.gz"
  'fleet-orbit'
  'fleet-orbit-cleanup.hook'
  'fleet-orbit-config'
  'fleet-orbit.service'
)
sha512sums=(
  '{{ metadata.release_sha512sum }}'
  '{{ sha512sum("fleet-orbit") }}'
  '{{ sha512sum("fleet-orbit-cleanup.hook") }}'
  '{{ sha512sum("fleet-orbit-config") }}'
  '{{ sha512sum("fleet-orbit.service") }}'
)

build() {
  cd "$pkgname-v$pkgver"

  mkdir -p build
  go mod download
  isodate=$(TZ=GMT date +"%Y-%m-%dT%H:%M:%SZ")
  CGO_ENABLED=0 go build \
    -o build/ \
    -trimpath \
    -buildvcs=false \
    -ldflags "-s -w -X github.com/fleetdm/fleet/v4/orbit/pkg/build.Version=v$pkgver -X github.com/fleetdm/fleet/v4/orbit/pkg/build.Commit={{ metadata.tag_info.commit_sha }} -X github.com/fleetdm/fleet/v4/orbit/pkg/build.Date=$isodate" \
    ./orbit/cmd/orbit/
}

package() {
  install -Dm644 'fleet-orbit-config' "$pkgdir/etc/default/fleet-orbit"
  install -Dm644 'fleet-orbit.service' "$pkgdir/usr/lib/systemd/system/fleet-orbit.service"
  install -Dm644 'fleet-orbit-cleanup.hook' "$pkgdir/usr/share/libalpm/hooks/fleet-orbit-cleanup.hook"
  
  install -Dm644 "$pkgname-v$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -Dm755 "$pkgname-v$pkgver/build/orbit" "$pkgdir/opt/fleet-orbit/bin/orbit/linux/stable/orbit"
  ln -s "./linux/stable/orbit" "$pkgdir/opt/fleet-orbit/bin/orbit/orbit"

  install -Dm755 "fleet-orbit" "$pkgdir/usr/bin/fleet-orbit"

  mkdir -p "$pkgdir/opt/fleet-orbit/bin/osqueryd/linux/stable/"
  ln -s /usr/bin/osqueryd "$pkgdir/opt/fleet-orbit/bin/osqueryd/linux/stable/osqueryd"
}
