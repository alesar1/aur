# Maintainer  : Chris Billington <chrisjbillington@gmail.com>
pkgname=python-numpy-mkl-bin
_pkgname=numpy-base
_pkgver=1.18.5
_build=py38hde5b4d6_0
_build_number=0
pkgver="${_pkgver}.${_build_number}.${_build//_/.}"
pkgrel=2
pkgdesc="Scientific tools for Python - with Intel MKL - prebuilt binaries from Anaconda"
arch=('x86_64')
url="https://www.numpy.org/"
license=('custom')
provides=('python-numpy' 'python-numpy-mkl')
conflicts=('python-numpy')
depends=('python-mkl-service')
source=("https://repo.anaconda.com/pkgs/main/linux-64/${_pkgname}-${_pkgver}-${_build}.tar.bz2")
sha256sums=('5d0e0e79ebd4ef426f0ae07c7eb16891a55468997f91405f7d28bdf33f422f9f')


prepare() {
    python -c 'import json; from pathlib import Path
for path_info in json.loads(Path("info", "paths.json").read_text())["paths"]:
    placeholder = path_info.get("prefix_placeholder")
    if placeholder is not None:
        path = Path(path_info["_path"])
        path.write_bytes(path.read_bytes().replace(placeholder.encode("utf8"), b"/usr"))'
}

package() {
  mkdir "${pkgdir}/usr"
  cp -drp --no-preserve=ownership "${srcdir}/bin" "${pkgdir}/usr/bin"
  cp -drp --no-preserve=ownership "${srcdir}/lib" "${pkgdir}/usr/lib"
  install -D -m 644 "${srcdir}/info/licenses/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
