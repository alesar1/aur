# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Jochen Immendoerfer <jochen dot immendoerfer at gmail dot com>
# Maintainer: Davi da Silva BÃ¶ger <dsboger@gmail.com>

pkgname=fmit
pkgver=1.0.14
pkgrel=1
pkgdesc="Free Music Instrument Tuner"
arch=('i686' 'x86_64')
url="https://github.com/gillesdegottex/fmit"
license=('GPL')
depends=('qt5-multimedia' 'fftw' 'hicolor-icon-theme' 'portaudio')
makedepends=('desktop-file-utils' 'xdg-utils' 'qt5-tools')
install="${pkgname}.install"
source=("https://github.com/gillesdegottex/fmit/archive/v${pkgver}.tar.gz")
sha512sums=('a73be2c0259f780757adb56231144c9f81fcfdcf3c34c6a5722acf68eb28e3d3e1f36b9135069aa92e259bb7f91907c3c050885b965f4a1c338c031f7e3ed0d5')

build() {
  _fmitdir="${srcdir}/${pkgname}-${pkgver}"
  mkdir -p "${_fmitdir}/build"
  cd "${_fmitdir}/build"
  lrelease ../${pkgname}.pro
  qmake-qt5 "PREFIX=/usr" "CONFIG+=acs_qt acs_alsa acs_jack acs_portaudio" ../${pkgname}.pro
  make
}

package() {
  _fmitdir="${srcdir}/${pkgname}-${pkgver}"

  cd "${_fmitdir}/build"
  make INSTALL_ROOT="${pkgdir}" install
}
