# Maintainer: √Årni Dagur <arnidg@protonmail.ch>

pkgname='xcp'
pkgver='0.5.1'
pkgrel=1
pkgdesc="An exctended 'cp', written in Rust"
arch=('x86_64')
url='https://github.com/tarka/xcp'
license=('GPL-3.0')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tarka/xcp/archive/$pkgver.tar.gz")
sha512sums=('334c6c5d7868cf17f2790b8e53d75b37bbd7c1b3e901799f86d916c6c1221d4c1512de6a221a7fe714e067ad31234e329a5c9bd09766d34134bcc33661b65dff')

build() {
  cd $pkgname-$pkgver
  cargo build --release --locked
}

check() {
  cd $pkgname-$pkgver
  cargo test --release --locked
}

package() {
  cd $pkgname-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/xcp
}
