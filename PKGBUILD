# Maintainer: Adrián Pérez de Castro <aperez@igalia.com>
pkgname='vault-ssh-helper'
pkgdesc='Allows using OTP authentication generated by a Vault server'
pkgver='0.1.0'
pkgrel='1'
url='https://github.com/hashicorp/vault-ssh-helper/'
arch=('x86_64' 'i686')
makedepends=('go')
depends=('glibc')
source=("${url}/archive/v0.1.0.tar.gz")
sha512sums=('56c3d8611214f396ba512e9104650b0c9f1af759eb6ea1055a0339aa965b5a819b0ec9cbd7720c5c1fec4b879689ed630a4b2f540814dbd3d8387297102479e9')

_srcpath='src/github.com/hashicorp/vault-ssh-helper'
prepare () {
	if [[ ! -r ${_srcpath} ]] ; then
		mkdir -p "$(dirname "${_srcpath}")"
		ln -s "$(pwd)/${pkgname}-${pkgver}" "${_srcpath}"
	fi

	export GOPATH="${srcdir}:$(pwd)"
	cd "${_srcpath}"
	make updatedeps NAME=${pkgname}
	go generate ./...
}

build () {
	export GOPATH="${srcdir}:$(pwd)"
	cd "${_srcpath}"
	go build -v -x -o "${srcdir}/vault-ssh-helper"
}

package () {
	install -Dm755 "${srcdir}/vault-ssh-helper" \
		           "${pkgdir}/usr/bin/vault-ssh-helper"
	install -Dm644 "${pkgname}-${pkgver}/README.md" \
		           "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
