# Maintainer: getzze <getzze at gmail dot com>
# Initially generated by pip2pkgbuild

pkgname='python-persisting-theory'
_module='persisting-theory'
pkgver='0.2.1'
pkgrel=5
pkgdesc="Registries that can autodiscover values accross your project apps"
url="http://code.agate.blue/agate/persisting-theory"
depends=('python')
makedepends=('python-setuptools')
license=('BSD')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_module::1}/$_module/$_module-$pkgver.tar.gz")
sha256sums=('00ff7dcc8f481ff75c770ca5797d968e8725b6df1f77fe0cf7d20fa1e5790c0a')

prepare() {
  cd "$srcdir/${_module}-${pkgver}"
  rm -rf {example,tests}
  sed -i "s#recursive-include example#recursive-exclude example *\nrecursive-exclude tests *#g" MANIFEST.in
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build
}

package() {
    depends+=()
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
