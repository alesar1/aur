# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
pkgname=kokkos
pkgver=3.6.00
pkgrel=1
pkgdesc="C++ performance portability programming ecosystem"
arch=(x86_64)
url="https://github.com/${pkgname}/${pkgname}"
license=('custom:BSD-3-clause')
depends=(bash hwloc)
makedepends=(cmake)
source=(${url}/archive/${pkgver}.tar.gz)
sha512sums=('8256f9d5e0a4b84ea27ea1c3901953d08db9827f89e1722990e13a12e48ff4d0ae7349dee934799aa93f0d850481846c4912d5dad838ed40ec99ae2f1481deac')

build() {
  cmake \
    -S ${pkgname}-${pkgver} \
    -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=TRUE \
    -DCMAKE_CXX_STANDARD=14 \
    -DCMAKE_CXX_COMPILER=g++ \
    -DKokkos_ENABLE_HWLOC=ON \
    -DKokkos_ENABLE_OPENMP=ON \
    -DKokkos_ENABLE_TESTS=ON \
    -DKokkos_ENABLE_EXAMPLES=ON \
    -Wno-dev
  cmake --build build --target all
}

check() {
  ctest --test-dir build
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # https://github.com/kokkos/kokkos/issues/4983
  cd "${pkgdir}"
  rm usr/include/CMakeLists.txt
  rm usr/include/impl/CMakeLists.txt
}
