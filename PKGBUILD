# Contributor: Marcell Meszaros < marcell.meszaros AT runbox.eu >
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Generated by gem2arch (https://github.com/anatol/gem2arch)

_gemname='glib2'
pkgname="ruby-$_gemname-3.4"
pkgver=3.4.9
pkgrel=1
pkgdesc='Ruby/GLib2 is a Ruby binding of GLib-2.x.'
arch=('x86_64')
url="https://rubygems.org/gems/${_gemname}/versions/${pkgver}"
license=('LGPL2.1')
depends=(
  'glib2>=2.48'
  'ruby'
  'ruby-native-package-installer>=1.0.3'
  'ruby-pkg-config>=1.3.5'
)
options=('!emptydirs')
source=("https://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
b2sums=('53e51050f0c519bc6ef58ffe2112354ecfd77c5ec664ebeacb42e53091540949bea26b20d6e765c8cc859c9678dd71e4468c03781debe282396e9369a44feafc')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  local _platform="$(gem env platform | cut -d':' -f2)"
  local _extension_api_version="$(ruby -e'puts Gem.extension_api_version')"
  gem install --no-document --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "${_gemname}-${pkgver}.gem"
  rm "$pkgdir/$_gemdir/cache/${_gemname}-${pkgver}.gem" \
      "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/ext/glib2/"*.o \
      "${pkgdir}/${_gemdir}/extensions/${_platform}/${_extension_api_version}/${_gemname}-${pkgver}/gem_make.out" \
      "${pkgdir}/${_gemdir}/extensions/${_platform}/${_extension_api_version}/${_gemname}-${pkgver}/mkmf.log" \
      "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/ext/glib2/Makefile"
}
