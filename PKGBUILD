# Maintainer: Joan Bruguera Mic√≥ <joanbrugueram@gmail.com>
pkgname='tracking'
pkgdesc='Analyze how the behavior of a parallel application evolves through different scenarios (from BSC).'
pkgver='2.6.12.20200718'
pkgrel='2'
arch=('i686' 'x86_64')
url='https://www.bsc.es/discover-bsc/organisation/scientific-structure/performance-tools'
license=('GPL2')
depends=(wxparaver clusteringsuite python-pillow python-wxpython python-matplotlib)
source=("https://ftp.tools.bsc.es/$pkgname/$pkgname-${pkgver%.*}-src.tar.bz2")
sha512sums=(7942b03025ed5506e320a22cbb80f6fee21395deae0317f12a3e8cb3400fb8324a2b39bfc29febb3b8544cd0378f414cc80bdaa826e5bb95d6480023d5c36dc1)

prepare() {
	cd "$srcdir/$pkgname-${pkgver%.*}"
	# Buildfix: wxparaver 4.10.0-20220119 renames Window to Timeline but tracking hasn't been updated yet
	sed -i '1s/^/struct Timeline; typedef Timeline Window;\n/' src/CallersHistogram.cpp
}

build() {
	cd "$srcdir/$pkgname-${pkgver%.*}"

	./configure \
		--prefix=/usr \
		--with-paraver=/usr

	make
}

package() {
	cd "$srcdir/$pkgname-${pkgver%.*}"

	# WORKAROUND: Specifying the install directory using DESTDIR does not work
	make prefix="$pkgdir/usr/" install
}
