# Maintainer:  Alex Mekkering <amekkering at gmail dot com>
# Contributor: Bjoern Franke <bjo@nord-west.org>

pkgname=tvheadend

pkgver=4.0.10
pkgrel=1
pkgdesc="TV streaming server for Linux"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://tvheadend.org/projects/tvheadend"
license=('GPL3')
depends=('avahi' 'linuxtv-dvb-apps' 'ffmpeg2.8' 'uriparser')
makedepends=('git' 'wget' 'python2')
optdepends=('xmltv: For an alternative source of programme listings'
	    'libiconv: For conversion of character encodings')
provides=('tvheadend')
conflicts=('tvheadend-git')
install=tvheadend.install

source=("https://github.com/tvheadend/tvheadend/archive/v$pkgver.tar.gz" 
	'tvheadend.service'
)
sha512sums=('419d3be0d6f9d41925d0185eae04d321efe25d756c6b033d91a363b3c150516a0d47c01adab9ac3148ae9463be2733f100a1779f3d9dd458c2e575e3fac3ce16'
            '456084a08ce0e02a4a32d67a8a7937d45695cb4b08b29297ead8a7e9d2a92963335f653f56d4c9346f4b2729868408fafbcd4caebf9fc8cdaa90171514cf4a57')
 
prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}
    env PKG_CONFIG_PATH=/usr/lib/ffmpeg2.8/pkgconfig ./configure --prefix=/usr --python=python2
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="$pkgdir/" install
    chmod 0644 ${pkgdir}/usr/share/man/man1/tvheadend.1
    install -D -m 644 "$srcdir/tvheadend.service" "$pkgdir/usr/lib/systemd/system/tvheadend.service"
}
