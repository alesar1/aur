# Maintainer: Taboon Egon <te451 -_AT_- netcourrier -_DOT_- com>
# Contributor: Narrat <address??? at domain??? dot tld???>
# Contributor: der_fenix <derfenix -_AT_- gmail -_DOT_- com>
pkgname=semantik
pkgver=1.2.6
pkgrel=2
pkgdesc="mindmapping-like tool for document generation (reports, thesis, etc)"
arch=('x86_64')
url="https://waf.io/semantik.html"
license=('GPL3')
depends=('python' 'qt5-webengine' 'qt5-svg' 'qt5-tools' 'kdelibs4support' 'kio' 'python-pygments')
makedepends=('shared-mime-info' 'desktop-file-utils')
optdepends=('minted: syntax highlighting support in documents generated by LaTeX')
source=("https://waf.io/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('3b33b4b4ab92158598d05b0d38cba2bca2b062927989249466bca668cfb7a635')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CXXFLAGS='-I/usr/include/KF5/KIOCore -I/usr/include/KF5/KAuth -I/usr/include/KF5/KIOWidgets -I/usr/include/KF5/KIconThemes -I/usr/include/KF5/KConfigWidgets -I/usr/include/KF5/KXmlGui -I/usr/include/KF5/KCoreAddons -I/usr/include/KF5/KConfigGui -I/usr/include/KF5/KConfigCore -I/usr/include/KF5/KWidgetsAddons -I/usr/include/KF5/KI18n -I/usr/include/KF5/SonnetUi' ./waf configure clean build
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./waf install --destdir=${pkgdir} --nomimes --noldconfig --nogtkicons
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/example.sem" "$pkgdir/usr/share/doc/$pkgname/example.sem"
}
