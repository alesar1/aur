# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-sweep
pkgver=0.6.2
pkgrel=1
pkgdesc="A cargo subcommand for cleaning up unused build files generated by Cargo"
arch=('x86_64')
url="https://github.com/holmgr/cargo-sweep"
license=('MIT')
depends=('gcc-libs' 'rust')
makedepends=('git')
options=('!lto')
_commit='e3b67bd051910f57df8ab044512a13a980d88c06'
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"
  cargo build --frozen --release
}

check() {
  cd "$pkgname"
  cargo test --frozen
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/cargo-sweep

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
