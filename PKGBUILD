# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
_base=tcxreader
pkgname=python-${_base}
pkgdesc="Reader for Garmin's TCX file format"
pkgver=0.3.10
pkgrel=1
arch=(any)
url="https://github.com/alenrajsp/${_base}"
license=(MIT)
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=(${url}/releases/download/v${pkgver}/${_base}-${pkgver}.tar.gz)
sha512sums=('bcc2a5af67e676456e026c7caf90ac65543ed2aa6d51bbdf9668ee5fc4dee7ebd8eb9a008c5f0c4e0c73838600c5bd331d9a2bdd5d911dc84e27fc700ce4c53e')

build() {
  cd ${_base}-${pkgver}
  python setup.py build
}

check() {
  cd ${_base}-${pkgver}
  python -m pytest
}

package() {
  cd ${_base}-${pkgver}
  PYTHONPYCACHEPREFIX="${PWD}/.cache/cpython/" python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
