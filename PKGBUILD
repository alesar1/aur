# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: Excitable Snowball <excitablesnowball@gmail.com>
_base=sphinxcontrib-katex
pkgname=python-${_base}
pkgver=0.8.6
pkgrel=2
pkgdesc="A Sphinx extension for rendering math in HTML pages"
arch=(any)
url="https://github.com/hagenw/${_base}"
license=(MIT)
depends=(python-sphinx)
makedepends=(python-setuptools)
source=(${url}/archive/v${pkgver}.tar.gz)
sha512sums=('27950f14ac6d45534cfd0c302a5b9a4e944dae4ec851f241ebcedd2c737bd40c2573331b9f01120f784b6ff66adf0bb483071131514862a88cec4e092ee8d907')

prepare() {
  # https://github.com/hagenw/sphinxcontrib-katex/pull/73/files
  sed -i 's/packages = find:/packages = find_namespace:/' ${_base}-${pkgver}/setup.cfg
  rm ${_base}-${pkgver}/sphinxcontrib/__init__.py
}

build() {
  cd ${_base}-${pkgver}
  python setup.py build
}

check() {
  cd ${_base}-${pkgver}
  python -m sphinx docs docs/_build/ -c docs/ -b html -W
  python -m sphinx docs docs/_build/ -c docs/ -b latex -W
}

package() {
  cd ${_base}-${pkgver}
  PYTHONPYCACHEPREFIX="${PWD}/.cache/cpython/" python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
