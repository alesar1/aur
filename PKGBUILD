# Maintainer : Taiki Sugawara <buzz.taiki@gmail.com>
pkgname=gcviewer-bin
_pkgname=gcviewer
pkgver=1.36
pkgrel=1
pkgdesc="GCViewer is a little tool that visualizes verbose GC output generated by Sun, IBM, HP and BEA Java Virtual Machines."
arch=('any')
url="https://github.com/chewiebug/GCViewer"
license=('LGPL')
depends=('java-runtime')
makedepends=('imagemagick')
provides=('gcviewer')
conflicts=('gcviewer' 'gcviewer-git')
source=("http://downloads.sourceforge.net/project/gcviewer/$_pkgname-$pkgver.jar"
        "https://raw.githubusercontent.com/chewiebug/GCViewer/$pkgver/README.md"
        "https://raw.githubusercontent.com/chewiebug/GCViewer/$pkgver/src/main/resources/images/gcviewericon.gif"
        'gcviewer.sh'
        'gcviewer.desktop')
sha256sums=('5e6757735903d1d3b8359ae8fabc66cdc2ac6646725e820a18e55b85b3bc00f4'
            'c2415418801329f0162a5ff46c3cd4d1240f0dd621848a0b5d8cf35f05af8eed'
            '016cb8684edccbef21834a274a5e3c9226ef19a2cb88fc6f317757d53661e3ba'
            '20316cd8975215966d9983aab9cc61a1a95acdbd397d30db6ad1da7f2ecc2e0b'
            '255f2a7b966832b98b0e8683d3a2b97a3a71bd354000d226ab21500ec52e8a91')
noextract=("$_pkgname-$pkgver.jar")

build() {
  convert "$srcdir/gcviewericon.gif" "$srcdir/gcviewericon.png"
}

package() {
  install -D -m644 "$srcdir/$_pkgname-$pkgver.jar"  "$pkgdir/usr/share/java/$_pkgname/$_pkgname.jar"
  install -D -m644 "$srcdir/README.md"  "$pkgdir/usr/share/doc/$_pkgname/README.md"
  install -D -m644 "$srcdir/gcviewericon.png" "$pkgdir/usr/share/pixmaps/$_pkgname.png"
  install -D -m755 "$srcdir/gcviewer.sh" "$pkgdir/usr/bin/gcviewer"
  install -D -m644 "$srcdir/gcviewer.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
