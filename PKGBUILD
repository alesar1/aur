# Maintainer: Ivan Shapovalov <intelfx@intelfx.name>

pkgbase=python-pysaml2
pkgname=($pkgbase python2-pysaml2)
pkgver=4.4.0
pkgrel=1
pkgdesc='Python implementation of SAML Version 2'
license=('Apache')
arch=('any')
url='https://pypi.python.org/pypi/pysaml2'
makedepends=('python-setuptools'
             'python2-setuptools')
source=("https://pypi.python.org/packages/b2/d8/e294546b3ef6dad5cc250939b191a5230780f6bcb1faaef0bb92fdab5308/pysaml2-4.4.0.tar.gz")
md5sums=('401a34fd919f0f95efaa1e34a59bcbf9')

prepare() {
	cp -a "pysaml2-$pkgver"{,-python2}
}

build() {
	cd "$srcdir/pysaml2-$pkgver"
	python setup.py build

	cd "$srcdir/pysaml2-$pkgver-python2"
	python2 setup.py build
}

package_python-pysaml2() {
	depends+=('python' 'python-future'  'python-requests'  'python-zope-interface'  'python-pycryptodomex'  'python-pytz'  'python-pyopenssl'  'python-dateutil'  'python-six')

	cd "pysaml2-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-pysaml2() {
	depends+=('python2' 'python2-future'  'python2-requests'  'python2-zope-interface'  'python2-pycryptodomex'  'python2-pytz'  'python2-pyopenssl'  'python2-dateutil'  'python2-six')

	cd "pysaml2-$pkgver-python2"
	python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

	# rename all executables
	find "$pkgdir/usr/bin" -type f -exec mv {} {}2 \;
}

# vim: set ts=4 sw=4 tw=0 ft=sh :
