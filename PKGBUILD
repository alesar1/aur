
# Maintainer: Solomon Choina <shlomochoina@gmail.com>
pkgname=haskell-gi-cairo-connector
_hkgname=gi-cairo-connector
pkgver=0.0.1
pkgrel=1
pkgdesc="Foundation for libraries generated by haskell-gi"
url=https://hackage.haskell.org/package/gi-cairo-connector
license=('BSD')
arch=('i686' 'x86_64')
depends=('ghc-libs' 'haskell-utf8-string' 'haskell-gi-cairo-render' 'haskell-gi-cairo')
makedepends=('ghc' 'git')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('cf5e04e491dd9028e411502db9bed4899487c6234f6974ec320e4d89459c0a52')

prepare(){
  cd $_hkgname-$pkgver
  echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
	cd "${srcdir}/${_hkgname}-${pkgver}"
	runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${_hkgname}-$pkgver
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s /usr/share/doc/${pkgname}/html "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
