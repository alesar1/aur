
# Maintainer: Solomon Choina <shlomochoina@gmail.com>
pkgname=haskell-gi-cairo-render
_hkgname=gi-cairo-render
pkgver=0.0.1
pkgrel=3
pkgdesc="Foundation for libraries generated by haskell-gi"
url=https://hackage.haskell.org/package/gi-cairo-render
license=('BSD')
arch=('i686' 'x86_64')
depends=('ghc-libs' 'haskell-utf8-string' 'haskell-gi-base')
makedepends=('ghc' 'git' 'c2hs')
source=(git+https://github.com/taffybar/gi-cairo-render#commit=480dca243876a68db6e62ded80ce60292ebabfb5)
sha256sums=('SKIP')

prepare(){
  cd $_hkgname/$_hkgname
  echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
	cd "${srcdir}/${_hkgname}/${_hkgname}"
	runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${_hkgname}/${_hkgname}
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s /usr/share/doc/${pkgname}/html "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
