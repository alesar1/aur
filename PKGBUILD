# Maintainer: TÃ©rcio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>

pkgname=r3d-sdk
pkgver=8.1.1
pkgrel=1
arch=('x86_64')
pkgdesc="SDK to load and decode R3D files generated by RED cameras"
url="https://www.red.com/developers"
license=('custom')
depends=('gcc-libs' 'util-linux-libs')
makedepends=('poppler')
source=("https://downloads.red.com/software/sdk/R3D/${pkgver%.*}/R3DSDKv${pkgver//./_}.zip")
sha512sums=('bf67e3ffc78fc2361605f816b9b31e70a1d7fb14deb61bddf2346d427afe92c628a7007164fe9bf7e8e4c9cb9d1960caad27ff3f1ccd9773db2a2701995a962b')

prepare(){
  pdftotext -layout "R3DSDKv${pkgver//./_}/SDK License Agreement.pdf" 2>&-
  mv "R3DSDKv${pkgver//./_}/SDK License Agreement.txt" "$srcdir"
}

package() {
  install -d "$pkgdir/opt"
  install -d "$pkgdir/usr/share/licenses/$pkgname"

  cp -dr --no-preserve=ownership "R3DSDKv${pkgver//./_}" \
                                 "$pkgdir/opt/R3DSDK"

  cp --no-preserve=ownership "$srcdir/SDK License Agreement.txt" \
                             "$pkgdir/usr/share/licenses/$pkgname"
}
