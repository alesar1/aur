# Maintainer: tytan652 <tytan652@tytanium.xyz>
pkgname=obs-dir-watch-media
pkgver=0.5.3
_obsver=26.1.2
pkgrel=1
pkgdesc="adds a filter you can add to media source to load the oldest or newest file in a directory"
arch=('x86_64')
url="https://github.com/WarmUpTill/SceneSwitcher"
license=("GPL2")
depends=("obs-studio")
# Need to compile plugin in OBS compilation process
makedepends=(
  'cmake' 'libfdk-aac' 'libxcomposite' 'x264' 'vlc' 'swig' 'python'
  'luajit' 'ffmpeg' 'jansson' 'libxinerama' 'libxkbcommon-x11' 'mbedtls'
  'qt5-svg' 'qt5-x11extras' 'curl' 'jack' 'gtk-update-icon-cache'
)
source=(
  "$pkgname-$pkgver::git+https://github.com/exeldro/obs-dir-watch-media#commit=859eac497a3976919b4c40b04c270544ac5b26f5"
  "obs-studio-$_obsver.tar.gz::https://github.com/obsproject/obs-studio/archive/$_obsver.tar.gz"
  "fix_python_binary_loading.patch"
)
sha256sums=(
  'SKIP'
  'bc8b4c61ba4aae87b70c589a6a5b8538e4f03121b31e9b98a2071d9f6006b897'
  'bdfbd062f080bc925588aec1989bb1df34bf779cc2fc08ac27236679cf612abd'
)

prepare() {
  rm -rf obs-studio
  cd "obs-studio-$_obsver"
  patch -Np1 < "$srcdir"/fix_python_binary_loading.patch
  cd plugins
  cp -r "$srcdir/$pkgname-$pkgver" .
  echo "add_subdirectory($pkgname-$pkgver)" | tee -a CMakeLists.txt >/dev/null
}

# Need to compile plugin in OBS compilation process
build() {
  cd "obs-studio-$_obsver"
  cmake -B build \
  -DCMAKE_INSTALL_PREFIX='/usr' \
  -DBUILD_CAPTIONS=ON
  make -C build
}

package() {
  mkdir obs-studio
  mkdir -p "$pkgdir"/usr/lib/obs-plugins
  mkdir -p "$pkgdir"/usr/share/obs/obs-plugins

  cd "obs-studio-$_obsver"
  make -C build DESTDIR="$srcdir/install/" install
  cp -a "$srcdir"/install/usr/lib/obs-plugins/dir-watch-media.so "$pkgdir"/usr/lib/obs-plugins/
  cp -a "$srcdir"/install/usr/share/obs/obs-plugins/dir-watch-media "$pkgdir"/usr/share/obs/obs-plugins/
}