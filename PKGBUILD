# Maintainer: Adrián Pérez de Castro <aperez@igalia.com>
pkgname='vault-ssh-helper'
pkgdesc='Allows using OTP authentication generated by a Vault server'
pkgver='0.1.4'
pkgrel='1'
url='https://github.com/hashicorp/vault-ssh-helper/'
arch=('x86_64' 'i686')
license=('MPL')
makedepends=('go')
depends=('glibc')
source=("${url}/archive/v${pkgver}.tar.gz")
sha512sums=('9a7d8f31e5e6406bc8e735a10d866df85c2f8d5548d45d1a295a24339da429d598187cf24fc40b4d30fac70cdc35c7605a2398384e58170edaa96d54ea0954cd')

_srcpath='src/github.com/hashicorp/vault-ssh-helper'
prepare () {
	if [[ ! -r ${_srcpath} ]] ; then
		mkdir -p "$(dirname "${_srcpath}")"
		ln -s "$(pwd)/${pkgname}-${pkgver}" "${_srcpath}"
	fi

	export GOPATH="${srcdir}:$(pwd)"
	cd "${_srcpath}"
	make updatedeps NAME=${pkgname}
	go generate ./...
}

build () {
	export GOPATH="${srcdir}:$(pwd)"
	cd "${_srcpath}"
	go build -v -o "${srcdir}/vault-ssh-helper"
}

package () {
	install -Dm755 "${srcdir}/vault-ssh-helper" \
		           "${pkgdir}/usr/bin/vault-ssh-helper"
	install -Dm644 "${pkgname}-${pkgver}/README.md" \
		           "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
