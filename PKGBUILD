# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
_base=syrupy
pkgname=python-${_base}
pkgdesc="The sweeter pytest snapshot plugin"
pkgver=3.0.2
pkgrel=3
arch=(any)
url="https://github.com/tophat/${_base}"
license=(Apache)
depends=(python-pytest python-colored)
makedepends=(python-build python-installer python-poetry-core)
source=(https://pypi.org/packages/source/${_base::1}/${_base}/${_base}-${pkgver}.tar.gz)
sha512sums=('a03568894460d9a3f525be5e499fd9067e8519b60934e406132a5b7afbea4e3ac848dd0c006bb135fc79f7fd2f4c1e0467c4194b19553039f3c3e6006fc2e06f')

build() {
  cd ${_base}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${_base}-${pkgver}
  PYTHONPYCACHEPREFIX="${PWD}/.cache/cpython/" python -m installer --destdir="${pkgdir}" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  ln -s "${site_packages}/${_base}-${pkgver}.dist-info/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
