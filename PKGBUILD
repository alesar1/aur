# Generated by debtap
# Maintainer: elmo-space
# Contributor: elmo-space
pkgname=stride-desktop
pkgver=1.22.76
pkgrel=2
pkgdesc="Stride is a cloud-based team business communication and collaboration tool - Desktop client"
arch=('x86_64')
url="www.stride.com"
license=('GPL')
depends=('alsa-lib' 'atk' 'cairo' 'dbus' 'desktop-file-utils' 'electron' 'expat' 'fontconfig' 'freetype2' 'gconf' 'gdk-pixbuf2' 'glib2' 'gtk2' 'gvfs' 'hicolor-icon-theme' 'libappindicator-gtk2' 'libcups' 'libnotify' 'libx11' 'libxcb' 'libxcomposite' 'libxcursor' 'libxdamage' 'libxext' 'libxfixes' 'libxi' 'libxrandr' 'libxrender' 'libxss' 'libxtst' 'nspr' 'nss' 'pango' 'xdg-utils')
optdepends=('kde-cli-tools'
            'kdebase-runtime'
            'opera'
            'trash-cli'
            'gnome-keyring'
            'libgnome-keyring'
            'lsb-release'
            'pulseaudio')
makedepends=('patchelf')
options=('!strip' 'staticlibs')
source=("https://packages.atlassian.com/stride-apt-client/pool/stride_${pkgver}_amd64.deb"
        "https://archive.archlinux.org/packages/g/glibc/glibc-2.27-3-x86_64.pkg.tar.xz")
noextract=("glibc-2.27-3-x86_64.pkg.tar.xz")
sha256sums=('ae336753cd845e1981942f148a13e51974b564e88d313e484406b983202f2070'
            'a9e1b18d7f613be660556dbd6883781e88a0f5113230147e230d3e2f268792dc')

package(){
    # Extract package data
    tar xf data.tar.xz -C "${pkgdir}"

    # Move license
    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    mv "${pkgdir}/usr/share/doc/stride/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    ln -s "/usr/share/licenses/${pkgname}/LICENSE" "${pkgdir}/usr/lib/stride/LICENSE"

    # Remove all unnecessary stuff
    rm -rf "${pkgdir}/usr/share/doc"
    rm -rf "${pkgdir}/usr/share/lintian"
	 
    # Patch stride to use glibc 2.27
    _idir="/usr/lib/stride"
    _pdir="${pkgdir}${_idir}"
    _pbin="${_pdir}/stride"
    mkdir -p "$_pdir/glibc"
    tar -xJf "glibc-2.27-3-x86_64.pkg.tar.xz" -C "$_pdir/glibc"
    rm "$_pdir/glibc/"{.BUILDINFO,.INSTALL,.MTREE,.PKGINFO}
    patchelf --set-interpreter "$_idir/glibc/usr/lib/ld-linux-x86-64.so.2" "$_pbin"
    patchelf --set-rpath "$_idir:$_idir/glibc/usr/lib" "$_pbin"

}
