# Maintainer: Forest Crossman <cyrozap at gmail dot com>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jeremy Newton (Mystro256) <alexjnewt@gmail.com>

_pkgname=dolphin-emu
pkgname=${_pkgname}-wayland
pkgver=5.0.r17269.48c9c224cf
pkgrel=1
epoch=1
pkgdesc='A Gamecube / Wii / Triforce emulator, patched with experimental support for Wayland'
arch=(x86_64)
url=https://dolphin-emu.org
license=(GPL2)
depends=(
  alsa-lib
  bluez-libs
  enet
  gcc-libs
  glibc
  hidapi
  libavcodec.so
  libavformat.so
  libavutil.so
  libcurl.so
  libevdev
  libfmt.so
  libgl
  libminiupnpc.so
  libpulse
  libswscale.so
  libudev.so
  libusb-1.0.so
  libx11
  libxi
  libxrandr
  lzo
  mbedtls
  minizip-ng
  pugixml
  qt6-base
  sfml
  zlib
)
makedepends=(
  cmake
  git
  ninja
  python
)
optdepends=('pulseaudio: PulseAudio backend')
options=(!emptydirs)
conflicts=(${_pkgname})
provides=(${_pkgname})
_commit=48c9c224cf9f82f0f9f2690b7cc6283d7448480c
source=(
  dolphin-emu::git+https://github.com/dolphin-emu/dolphin.git#commit=${_commit}
  git+https://github.com/randy408/libspng.git
  git+https://github.com/KhronosGroup/SPIRV-Cross.git
  git+https://github.com/zlib-ng/zlib-ng.git
  dolphin-emu-wayland-support.patch  # Generated by rebasing https://github.com/dolphin-emu/dolphin/pull/8727
  dolphin-emu-wayland.desktop
)
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        '89f004d45f607383ac31170a03faf0e4ed5a6b7df9278a1e3534782a0adc593ef858434e7afa186854c44e60a65c324de09c4d433dc4c8aeaaccf585aedd7f3c'
        '39ff2c0abb4b05583e3e207380fee5c89f096bd31b7fc26de2efe4ad30915b933440515cb34592f7ee48adf3383641138b21f90cfeba3be822424a1a4583d5ae')

prepare() {
  cd dolphin-emu

  patch -p1 < ${srcdir}/dolphin-emu-wayland-support.patch

  for submodule in Externals/{libspng/libspng,spirv_cross/SPIRV-Cross,zlib-ng/zlib-ng}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule##*/}
    git submodule update ${submodule}
  done
}

pkgver() {
  cd dolphin-emu
  git describe | sed 's/-/.r/; s/-g/./'
}

build() {
  cmake -S dolphin-emu -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDISTRIBUTOR=archlinux.org \
    -DUSE_MGBA=OFF \
    -DUSE_SHARED_ENET=ON \
    -DENABLE_WAYLAND=ON \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 dolphin-emu/Data/51-usb-device.rules -t "${pkgdir}"/usr/lib/udev/rules.d/
  install -Dm 644 dolphin-emu-wayland.desktop -t "${pkgdir}"/usr/share/applications/
  rm -rf "${pkgdir}"/usr/{include,lib/libdiscord-rpc.a}
}

# vim: ts=2 sw=2 et:
