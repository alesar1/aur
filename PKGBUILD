# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
_base=treestamps
pkgname=python-${_base}
pkgver=0.3.1
pkgrel=1
pkgdesc="Create timestamp records for recursive operations on directory trees"
url="https://github.com/ajslater/${_base}"
depends=(python-ruamel-yaml python-termcolor)
makedepends=(python-setuptools)
license=(GPL3)
arch=(any)
source=(https://pypi.org/packages/source/${_base::1}/${_base}/${_base}-${pkgver}.tar.gz)
sha512sums=('9405ba6a7d77713018494d3095bdd4b82a0fa0fba5ac916a821ae75b4b3292728579ab0dee1fe6d8a8cd24a9bc5417b013ed0d9e20188fa92fe8a72d13dbd933')

build() {
  cd ${_base}-${pkgver}
  python setup.py build
}

check() {
  cd ${_base}-${pkgver}
  PYTHONPATH="${PWD}/build/lib:${PYTHONPATH}" python tests/unit/*.py
}

package() {
  cd ${_base}-${pkgver}
  PYTHONPYCACHEPREFIX="${PWD}/.cache/cpython/" python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
}
