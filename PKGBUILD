# Maintainer: Andy Botting <andy@andybotting.com>

pkgname=python-barbicanclient
pkgver='4.10.0'
pkgrel='1'
pkgdesc='Client library for the Barbican Key Management API'
arch=('any')
url="https://docs.openstack.org/$pkgname/"
license=('Apache')
depends=('python-pbr' 'python-prettytable' 'python-requests' 'python-six'
         'python-cliff' 'python-keystoneauth1' 'python-oslo-i18n'
         'python-oslo-serialization' 'python-oslo-utils')
checkdepends=('python-fixtures' 'python-requests-mock' 'python-mock'
              'python-testrepository' 'python-testtools' 'python-oslotest'
              'python-nose' 'python-oslo-config' 'python-openstackclient')
source=("https://github.com/openstack/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('3c241449e9f6e1c899c7c11f78bfd713f144fbaa511aeb989d52fdcfe54e04228f419231f7152b7b2d01d2d0509cc1840dc15a26ff07ec3b9051b8e354681ef2')

export PBR_VERSION=$pkgver

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  sed -i 's/assertItemsEqual/assertCountEqual/g' barbicanclient/tests/v1/*.py
  stestr run
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
