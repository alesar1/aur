# Maintainer: Nikola TasiÄ‡ <nik at 7aske dot com>
pkgname="rgs"
pkgver="1.14.2"
pkgrel=2
pkgdesc="Batch git repository analysis tool"
arch=('x86_64')
url="https://github.com/7aske/rgs"
license=('GPL2')
depends=('libgit2')
makedepends=('cargo')
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)

prepare() {
	cd $pkgname-$pkgver
    	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd $pkgname-$pkgver
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --release --locked --all-features --frozen
}

package() {
	cd $pkgname-$pkgver
	install -Dm 755 target/release/${pkgname} -T ${pkgdir}/usr/bin/rgs
	install -Dm 755 target/release/${pkgname} -T ${pkgdir}/usr/bin/cgs
	install -Dm 644 README.md -t $pkgdir/usr/share/doc/${pkgname}
	install -Dm 644 LICENSE -t $pkgdir/usr/share/licenses/${pkgname}
}

sha256sums=('1348544d8415a131998fb97bfa3b4dac47da10a885ea7ec2d7fc5213e1a9852f')
