# Maintainer: Andr√© Koch-Kramer <koch-kramer@web.de>

pkgname=instaloader
pkgver=3.2.2
pkgrel=1
pkgdesc="Command line tool to download pictures, videos and metadata from Instagram"
arch=('any')
url="https://instaloader.readthedocs.io/"
license=('MIT')
groups=()
depends=('python>=3.5' 'python-requests>=2.4')
makedepends=('python-setuptools')
options=('!emptydirs')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/Thammus/instaloader/tar.gz/v$pkgver)
sha512sums=('fd1f225cc54917da0f547572cd066df19c82b3896e6788e24451a587d6558cbac1c6cec806302b0359563de95a65ac1b2adcdc62a008f04063b73a0a9cdb2c1b')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	python setup.py clean --all
	python setup.py build
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
	python setup.py install --root "$pkgdir/" --skip-build
}
