# Maintainer: Klaus Alexander Seiï¬†rup <klaus@seistrup.dk>
# -*- mode: sh -*-

pkgname=fuzzynote
pkgver=0.24.1
pkgrel=1
_pkgdate=1664609760
pkgdesc='Terminal-based, CRDT-backed, local-first, collaborative note-taking'
arch=('x86_64')
url='https://github.com/Sambigeara/fuzzynote'
license=('AGPL3')
makedepends=('go')
provides=('fuzzynote')
conflicts=('fuzzynote')
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/Sambigeara/$pkgname/archive/refs/tags/v$pkgver.tar.gz"
)

build() {
  cd "$pkgname-$pkgver" || exit 1

  # https://wiki.archlinux.org/title/Go_package_guidelines
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"

  go mod tidy

  go build \
    -buildmode=pie \
    -trimpath \
    -ldflags="-linkmode=external -X main.version=$pkgver -X main.date=$_pkgdate" \
    -mod=readonly \
    -modcacherw \
    -o bin/fzn ./cmd/term
}

package() {
  cd "$pkgname-$pkgver" || exit 1

  install -Dm0755 bin/fzn   "$pkgdir/usr/bin/fzn"
  install -Dm0644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

sha256sums=(
  'e14960e4a06782a34b97bfcc488c27876131169cbf4815b15321d41ee958147a'
)
sha512sums=(
  '2e354d80eb1c723c0af0934ab382e4eb7b81c8e7ab4db19317363e5c8df281fa72f1619caa14dce0137104580769e73c70c49575ee7564a7257a9559136dbcf6'
)
b2sums=(
  'bb40f748b2704aed0d6925cde4eb30372e6e42e5ce554550609b5e851da4e80630c952f2307650b140116747202bed85e4e0345f3298e3a84f55653b61a6a05b'
)

# eof
