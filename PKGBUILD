# Maintainer: Yellow <yellow@example.com>
# Maintainer: Sefa Eyeoglu <contact@scrumplex.net>
# Maintainer: txtsd <code@ihavea.quest>
# Contributor: Lenny McLennington <lennymclennington@protonmail.com>
# Contributor: Elijah Gregg <lovetocode999@tilde.team>
# Contributor: Miko <mikoxyzzz@gmail.com>
# Contributor: Cheru Berhanu <aur attt cheru doot dev>
# Contributor: dada513 <dada513@protonmail.com>

pkgname=prismlauncher
pkgver=1.4.2
pkgrel=1
pkgdesc="Minecraft launcher with ability to manage multiple instances."
arch=('i686' 'x86_64')
url="https://github.com/PlaceholderMC/PlaceholderMC"
license=('GPL3')
depends=('java-runtime' 'libgl' 'qt6-base' 'qt6-5compat' 'qt6-svg' 'qt6-imageformats' 'zlib' 'hicolor-icon-theme' 'quazip-qt6')
makedepends=('cmake' 'extra-cmake-modules' 'git' 'java-environment' 'scdoc')
optdepends=('glfw: to use system GLFW libraries'
            'openal: to use system OpenAL libraries'
            'visualvm: Profiling support'
            'xorg-xrandr: for older minecraft versions')
source=("https://github.com/PlaceholderMC/PlaceholderMC/releases/download/$pkgver/PlaceholderMC-$pkgver.tar.gz"
        "fix-qt6-4.patch::https://github.com/PlaceholderMC/PlaceholderMC/commit/7ccafdc99321e82ee0f504a573a5c978480374a2.patch")
sha256sums=('a66f25e0389815d2419a5b3aa1b85a390f14bbf3997c55c7da1ce4507b5aa511'
            '4215ec878ec7a5a63cc5710ef09a66f271d332542f4c6f5bb5f270a924dadcc7')

prepare() {
    cd "PlaceholderMC-$pkgver"

    patch -Np1 -i "../fix-qt6-4.patch"
}

build() {

  cmake -DCMAKE_BUILD_TYPE= \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DLauncher_BUILD_PLATFORM="archlinux" \
    -DLauncher_QT_VERSION_MAJOR="6" \
    -Bbuild -SPlaceholderMC-$pkgver
  cmake --build build
}

check() {
  cd "build"
  ctest .
}

package() {
  cd "build"
  DESTDIR="$pkgdir" cmake --install .
}
