# Maintainer: Carl George < arch at cgtx dot us >
# Contributor: Zohar Malamant <dotfloat gmail com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Martin Erik Werner ("arand") <martinerikwerner@gmail.com>

pkgname="redeclipse"
pkgver="1.5.6"
pkgrel="1"
pkgdesc="free, casual arena shooter"
arch=("i686" "x86_64")
url="http://redeclipse.net"
license=("ZLIB" "CCPL:by-sa" "CCPL:by" "custom")
makedepends=("ed" "sdl2_mixer" "sdl2_image" "enet")
depends=("sdl2_image" "sdl2_mixer" "enet" "hicolor-icon-theme")
source=("base-${pkgver}.tar.gz::https://github.com/red-eclipse/base/archive/v${pkgver}.tar.gz"
        "acerspyro-${pkgver}.tar.gz::https://github.com/red-eclipse/acerspyro/archive/v${pkgver}.tar.gz"
        "actors-${pkgver}.tar.gz::https://github.com/red-eclipse/actors/archive/v${pkgver}.tar.gz"
        "appleflap-${pkgver}.tar.gz::https://github.com/red-eclipse/appleflap/archive/v${pkgver}.tar.gz"
        "blendbrush-${pkgver}.tar.gz::https://github.com/red-eclipse/blendbrush/archive/v${pkgver}.tar.gz"
        "caustics-${pkgver}.tar.gz::https://github.com/red-eclipse/caustics/archive/v${pkgver}.tar.gz"
        "crosshairs-${pkgver}.tar.gz::https://github.com/red-eclipse/crosshairs/archive/v${pkgver}.tar.gz"
        "dziq-${pkgver}.tar.gz::https://github.com/red-eclipse/dziq/archive/v${pkgver}.tar.gz"
        "elyvisions-${pkgver}.tar.gz::https://github.com/red-eclipse/elyvisions/archive/v${pkgver}.tar.gz"
        "fonts-${pkgver}.tar.gz::https://github.com/red-eclipse/fonts/archive/v${pkgver}.tar.gz"
        "freezurbern-${pkgver}.tar.gz::https://github.com/red-eclipse/freezurbern/archive/v${pkgver}.tar.gz"
        "john-${pkgver}.tar.gz::https://github.com/red-eclipse/john/archive/v${pkgver}.tar.gz"
        "jojo-${pkgver}.tar.gz::https://github.com/red-eclipse/jojo/archive/v${pkgver}.tar.gz"
        "jwin-${pkgver}.tar.gz::https://github.com/red-eclipse/jwin/archive/v${pkgver}.tar.gz"
        "luckystrike-${pkgver}.tar.gz::https://github.com/red-eclipse/luckystrike/archive/v${pkgver}.tar.gz"
        "maps-${pkgver}.tar.gz::https://github.com/red-eclipse/maps/archive/v${pkgver}.tar.gz"
        "mayhem-${pkgver}.tar.gz::https://github.com/red-eclipse/mayhem/archive/v${pkgver}.tar.gz"
        "mikeplus64-${pkgver}.tar.gz::https://github.com/red-eclipse/mikeplus64/archive/v${pkgver}.tar.gz"
        "misc-${pkgver}.tar.gz::https://github.com/red-eclipse/misc/archive/v${pkgver}.tar.gz"
        "nieb-${pkgver}.tar.gz::https://github.com/red-eclipse/nieb/archive/v${pkgver}.tar.gz"
        "nobiax-${pkgver}.tar.gz::https://github.com/red-eclipse/nobiax/archive/v${pkgver}.tar.gz"
        "particles-${pkgver}.tar.gz::https://github.com/red-eclipse/particles/archive/v${pkgver}.tar.gz"
        "philipk-${pkgver}.tar.gz::https://github.com/red-eclipse/philipk/archive/v${pkgver}.tar.gz"
        "projectiles-${pkgver}.tar.gz::https://github.com/red-eclipse/projectiles/archive/v${pkgver}.tar.gz"
        "props-${pkgver}.tar.gz::https://github.com/red-eclipse/props/archive/v${pkgver}.tar.gz"
        "skyboxes-${pkgver}.tar.gz::https://github.com/red-eclipse/skyboxes/archive/v${pkgver}.tar.gz"
        "snipergoth-${pkgver}.tar.gz::https://github.com/red-eclipse/snipergoth/archive/v${pkgver}.tar.gz"
        "sounds-${pkgver}.tar.gz::https://github.com/red-eclipse/sounds/archive/v${pkgver}.tar.gz"
        "textures-${pkgver}.tar.gz::https://github.com/red-eclipse/textures/archive/v${pkgver}.tar.gz"
        "torley-${pkgver}.tar.gz::https://github.com/red-eclipse/torley/archive/v${pkgver}.tar.gz"
        "trak-${pkgver}.tar.gz::https://github.com/red-eclipse/trak/archive/v${pkgver}.tar.gz"
        "ulukai-${pkgver}.tar.gz::https://github.com/red-eclipse/ulukai/archive/v${pkgver}.tar.gz"
        "unnamed-${pkgver}.tar.gz::https://github.com/red-eclipse/unnamed/archive/v${pkgver}.tar.gz"
        "vanities-${pkgver}.tar.gz::https://github.com/red-eclipse/vanities/archive/v${pkgver}.tar.gz"
        "vegetation-${pkgver}.tar.gz::https://github.com/red-eclipse/vegetation/archive/v${pkgver}.tar.gz"
        "weapons-${pkgver}.tar.gz::https://github.com/red-eclipse/weapons/archive/v${pkgver}.tar.gz"
        "wicked-${pkgver}.tar.gz::https://github.com/red-eclipse/wicked/archive/v${pkgver}.tar.gz"
        "redeclipse-1.5.5-build-with-system-enet.patch")
sha256sums=('c6b8c9e3e0e9a7dc16329fdba3c580627cbc70bdd490a64b3154a3f02cec63eb'
            '203a5a41c33719f8cb1d07886271e8b992c016cf5b3756c7f915d162d6501c43'
            'd0889b7de6b3d7104a84fc4d4d6082a14421f866b5c5a8b791a52a8e5de4076d'
            '9c4e750445377463545f308eb2a2588b3609a80e2c67b2a02c8db722c20e702e'
            'd1c1be62474f3d3b1283e04b82247bf8161c66141437b5e51489780db54308f2'
            'c6117c28f6adfa87841d24a9d2932309f9f8782100c1519901b057479a5d855d'
            'bd7bf0eec1a73e5ad23d7b87da7fa418cd2565a1ba0a834032d516de21662af1'
            '9761f04fbb145bda40410dfacf31b80a5b6f4013ee441642283ee6bbf56eceb6'
            'bd41a7ab220fed384098dbe8e1558820d9bc65984607e4324b0b55592e0d0313'
            'bda426faeaf99f5d8328b0a91eefd1de3261cb27b3b3bcbf520acc9e9a984ee7'
            '152041c39022893b135cf0a9b30f4fdd30dd4cfe83c30646b6aafb747a795d2c'
            '3f23974ae3e90ef559f019508934973e5b3bcc1bd40b1f33af6bd09ec393ef1e'
            '01095931c275abce80e2ad31ff6743d88a4f7e2c5988afd40b5661c76f697f52'
            'f50b2f6e6ce2af212d34523293a1df147c33497370f2c2ab297c296863b8de56'
            'f996b3038bad00ba6064a283b34cc0a1f72d80837f40b9407df457a8b6ed09b5'
            '7e953df531ca16ad0b86bdc156306ce217d02cb5508ceae9d4940a7ee844fea5'
            '0d296d80f1d75d17c35200bf59b1533cb1b8273be2c1f93ca2558ce3b47d22ea'
            '799b24d8ead99f4687ab00e7990425d9acac6a4cbfa3c91469802dfafc678850'
            'c32121c8c657920dd02728f420d839280a98da51042a75f2fe37e810070d313b'
            '62327231120eec4ddd530a74bd1268c308808fa729e5aec1114ce799c272c3dc'
            '4843ff2f8a905925f81a70ce470e78b70912ca06a7d92015ec0b2789c5f662cb'
            'c9e99759811f03d35780e22bedf1d7ee3915d5a4fc8e855be3f79d144a35923f'
            'c84ca39a2bacfe930c5db5c6b3092dc61d94e13d2ac76dd217ecd15d9966ec59'
            '77c104a8b279431feb4710a9661fd5c53d84cbefe7d70d337f39a1549e6ae6c5'
            '1dfe772a689446ce070caa3bfb391fbd93f5e148eaa6760ccd3fcdfa687e7a3c'
            '72d7da097135187cd536beaa8e046d77601e928c81a86cb3f075ee400efb7753'
            'ad923e721f474f7cb172192fe5a62db98f0cd52f41df29875638ae2283ee0885'
            'b4e2dd34884e675451a56399ad0a353942aff13e9d68d935197b9ece7adcbb4a'
            'cbbecde25cf179129aef7be688d600f3078e43511c19a5d399610bebd30ca0fb'
            '85374201c81dfc0d6eb15899ba4e7ee0d0e8a525222866ac5ad2d9026ed60689'
            'cc0c9c68d2080c6e31142d524e805b968d4f521c260f16bbbe25d5c5c473e980'
            'ebd884b1d2afa9e4e16d533ff7949c32bece47e08b3c462a30ca87627704a879'
            '95b0e84aef90daef87008456a73adf517eac456ec66a7f6e2a5bad79386c4150'
            '63097b707170e8b3a3ecfee51b2e923c6539598fb702e0c376c4f8e3a81811ab'
            '367f74e897977a1321b0cd58ed6faf54118404de30cf7496ccdc775d6c73a308'
            'c84de334f068a75e2acdbfdc8a50d351c300fc6831f5f8a0fe314df34cd75f78'
            '7db7f5d5157e30d911f2df7720746148429c1e5c26337247bf7f1bdb8a186045'
            '394cfa3f64385f05935467c88243432425304ef73dabd96fc35dcd66742f7bf9')

prepare() {
    cd "${srcdir}/base-${pkgver}"
    for i in \
    acerspyro actors appleflap blendbrush caustics crosshairs dziq elyvisions \
    fonts freezurbern john jojo jwin luckystrike maps mayhem mikeplus64 misc \
    nieb nobiax particles philipk projectiles props skyboxes snipergoth sounds \
    textures torley trak ulukai unnamed vanities vegetation weapons wicked
    do
        rmdir "data/${i}"
        mv "${srcdir}/${i}-${pkgver}" "data/${i}"
    done
    patch -p0 -i "${srcdir}/redeclipse-1.5.5-build-with-system-enet.patch"
}

build() {
    cd "${srcdir}/base-${pkgver}"
    make -C src/ client server
}

package() {
    cd "${srcdir}/base-${pkgver}"
    make -C src/ DESTDIR="${pkgdir}" prefix=/usr system-install
    install -Dm 0644 "${srcdir}/base-${pkgver}/doc/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
    install -Dm 0644 "${srcdir}/base-${pkgver}/doc/all-licenses.txt" "${pkgdir}/usr/share/licenses/${pkgname}/all-licenses.txt"
}
