# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgdesc='Omron LUNA-I and LUNA-88K emulator'
pkgname=nono
pkgver=0.2.5
pkgrel=1
url=http://www.pastel-flower.jp/~isaki/nono
license=(custom)
arch=(x86_64)
depends=(libbsd libkqueue termcap wxgtk3)
makedepends=(bmake clang freebsd-mk gettext)
source=("${url}/archive/${pkgname}-${pkgver}.tar.gz")
b2sums=('55a886b2ab634a34244cf4cd149bc95ce9c7046437715209cb7c3b192c143f651fba20a9a6aec65352f204b9767f0dd1d5b90e1450accb408b8f14d6920c0555')

prepare ()
{
	# The configure script searches for <kqueue/sys/event.h> instead of the
	# regular location <sys/event.h> as used by the libkqueue package, so
	# provide a local directory with the expected layout.
	mkdir -p "${srcdir}/include/kqueue/"
	ln -vsnf /usr/include/sys "${srcdir}/include/kqueue/sys"
}

build ()
{
	cd "${pkgname}-${pkgver}"

	# Use Clang because GCC 11+ has some false positive warnings that cause
	# the compilation to fail due to -Werror. Plus, the BSDs are using Clang
	# nowadays so using Clang builds is more tested.
	./configure \
		CC=clang \
		CXX=clang++ \
		CPPFLAGS="${CPPFLAGS} -I${srcdir}/include" \
		WX_CONFIG=/usr/bin/wx-config-gtk3 \
		--prefix=/usr

	# The configuration generated by autoconf does NOT include CPPFLAGS,
	# so add the include directory for the mangled libkqueue headers now.
	sed -i "/^C\(C\|XX\)=/s,$, -I${srcdir}/include," \
		Makefile.cfg

	bmake -DNOTEST -m/usr/share/mk-freebsd
}

package ()
{
	cd "${pkgname}-${pkgver}"
	bmake -DNOTEST -m/usr/share/mk-freebsd DESTDIR="${pkgdir}" install
	install -Dm644 doc/nono-license.txt \
		"${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
