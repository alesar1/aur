# Maintainer: Stephan Springer <buzo+arch@Lini.de>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>

pkgname=procdump
pkgver=1.1.1
pkgrel=2
pkgdesc='Generate coredumps based off performance triggers'
arch=('x86_64')
url=https://github.com/Sysinternals/ProcDump-for-Linux
license=('MIT')
depends=('gdb')
makedepends=('gcc8')
source=("$url/archive/$pkgver/ProcDump-for-Linux-$pkgver.tar.gz")
sha256sums=('712be77d97f802ca7ad7eaaa521f7742c15db93af0c968c8b54e48e3ab341baa')

build() {
  cd ProcDump-for-Linux-$pkgver
  make -j1 CC=gcc-8
}

package() {
  cd ProcDump-for-Linux-$pkgver
  install -Dt "$pkgdir"/usr/bin bin/procdump
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 procdump.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/procdump LICENSE
}

# vim:set ts=2 sw=2 et:
