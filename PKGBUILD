# Maintainer: √Årni Dagur <arnidg@protonmail.ch>

pkgname='xcp'
pkgver='0.7.3'
pkgrel=1
pkgdesc="An exctended 'cp', written in Rust"
arch=('x86_64')
url='https://github.com/tarka/xcp'
license=('GPL-3.0')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tarka/xcp/archive/v$pkgver.tar.gz")
sha512sums=('b4e3ab9cbdb8ce6a860269de256b34c9769cb27c2bb3731354ae71b9a442df4ca44454e01959f98a916579e70dbf9c5c494d0a43aa64ea64c2da13c03d1bdbdd')

build() {
  cd $pkgname-$pkgver
  cargo build --release --locked
}

check() {
  cd $pkgname-$pkgver
  cargo test --release --locked
}

package() {
  cd $pkgname-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/xcp
}
