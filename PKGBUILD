# Maintainer: Felix Kauselmann <licorn at gmail dot com>
pkgname=(yacreader yacreaderlibraryserver)
pkgbase=yacreader
pkgver=9.10.0
_pkgext=2210304
pkgrel=1
pkgdesc="Comic reader for cross-platform reading and managing your digital comic collection."
arch=('i686' 'x86_64')
url="http://www.yacreader.com"
license=('GPL3')
depends=('qt5-base' 'qt5-multimedia' 'qt5-quickcontrols2' 'qt5-graphicaleffects' 'qt5-svg' 'hicolor-icon-theme' 'libunarr' 'libpdfium-nojs')
makedepends=('qt5-tools')
optdepends=(
    'qt5-imageformats: Support for extra image formats'
    'qrencode: YACReaderLibrary server info qr codes'
)
conflicts=('yacreader-bin' 'yacreaderlibraryserver-standalone' 'yacreader-git')
install='yacreader.install'
source=( "https://github.com/YACReader/yacreader/releases/download/${pkgver}/yacreader-${pkgver}.${_pkgext}-src.tar.xz"
               "translation.patch::https://github.com/YACReader/yacreader/commit/7ed8426caddee8b01a9aca54ac496401fa42d12f.patch")
sha256sums=('37ffe008f00f03d4cfdecdad792241d7da009d98af293b7462ed1bd9639c0281'
            'da8124c2d38c79ed9bdaf65167f74ceb782b068bba1e9ca64a2bddfa1285344b')

prepare() {
  cd $srcdir/$pkgbase-$pkgver.${_pkgext}/
  patch --forward --strip=1 --input="${srcdir}/translation.patch"
}

build() {
  cd $srcdir/$pkgbase-$pkgver.${_pkgext}/
  qmake-qt5 CONFIG+="pdfium server_bundled"
  make
}

package_yacreader() {
  cd $srcdir/$pkgbase-$pkgver.${_pkgext}/
  make INSTALL_ROOT="$pkgdir" sub-YACReader-install_subtargets sub-YACReaderLibrary-install_subtargets
}

package_yacreaderlibraryserver() {
  pkgdesc="Headless YACReaderLibrary server for use with YACReader for iOS."
  install=yacreaderlibraryserver.install
  cd $srcdir/$pkgbase-$pkgver.${_pkgext}/
  make INSTALL_ROOT="$pkgdir" sub-YACReaderLibraryServer-install_subtargets
}
