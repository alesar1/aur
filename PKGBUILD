# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=moonlight
_gitname=${pkgname}-pc
pkgver=0.7.4
pkgrel=1
pkgdesc="Open source implementation of NVIDIA's GameStream, as used by the NVIDIA Shield"
url="http://moonlight-stream.com"
arch=('x86_64' 'i686')
license=('GPL3')
depends=('java-runtime' 'sh')
makedepends=('apache-ant')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/moonlight-stream/${_gitname}/archive/v${pkgver}-r2.tar.gz)
sha512sums=('c4c9c810189d2977213e9f9d729201e1cce62418b0526fb23a7c7b49ef368a27924285b5db0f70a5a30974b0312883c684d8268e715da3da271785b1306e9086')

prepare() {
  cd ${_gitname}-${pkgver}-r2
  cat > ${pkgname} <<EOF
#!/bin/sh
exec /usr/bin/java -jar "/usr/share/java/${pkgname}/${pkgname}.jar" "\$@"
EOF
}

build() {
  cd ${_gitname}-${pkgver}-r2
  if [ "${CARCH}" == "x86_64" ]; then
    ant moonlight-lin64
  elif [ "${CARCH}" == "i686" ]; then
    ant moonlight-lin32
  fi
}

package() {
  cd ${_gitname}-${pkgver}-r2
  if [ "${CARCH}" == "x86_64" ]; then
    install -Dm 644 ./build/moonlight-lin64.jar "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar"
  elif [ "${CARCH}" == "i686" ]; then
    install -Dm 644 ./build/moonlight-lin32.jar "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar"
  fi
  install -Dm 755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim: ts=2 sw=2 et:
