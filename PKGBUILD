# Maintainer: TÃ©rcio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>

pkgname=r3d-sdk
pkgver=8.3.1
pkgrel=1
arch=('x86_64')
pkgdesc="SDK to load and decode R3D files generated by RED cameras"
url="https://www.red.com/developers"
license=('custom')
depends=('gcc-libs' 'util-linux-libs')
makedepends=('poppler')
options=('!strip')
source=("https://downloads.red.com/software/sdk/R3D/${pkgver%.*}/R3DSDKv${pkgver//./_}.zip")
sha512sums=('d6b91a9898245c44f134c9dd64875405bc396bc79c0e1c6b4eef255546d1732770d434be7d46649821840e44396195fd027fa6c24d71481e6fad816fb72ed09b')

prepare(){
  pdftotext -layout "R3DSDKv${pkgver//./_}/SDK License Agreement.pdf" 2>&-
  mv "R3DSDKv${pkgver//./_}/SDK License Agreement.txt" "$srcdir"
}

package() {
  install -d "$pkgdir/opt"
  install -d "$pkgdir/usr/share/licenses/$pkgname"

  cp -dr --no-preserve=ownership "R3DSDKv${pkgver//./_}" \
                                 "$pkgdir/opt/R3DSDK"

  cp --no-preserve=ownership "$srcdir/SDK License Agreement.txt" \
                             "$pkgdir/usr/share/licenses/$pkgname"
}
