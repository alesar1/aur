# Maintainer: Lucas Werkmeister <mail@lucaswerkmeister.de>
# Contributor: Dave Reisner <dreisner@archlinux.org>

pkgname=mkosi-git
pkgver=r44.96e64b4
pkgrel=1
pkgdesc="Build Legacy-Free OS Images"
arch=('any')
url="https://github.com/systemd/mkosi"
license=('LGPL2.1')
depends=('python')
makedepends=('python-setuptools')
optdepends=('dnf: build Fedora images'
            'debootstrap: build Debian or Ubuntu images'
            'arch-install-scripts: build Arch images'
            'xz: compress images with xz'
            'btrfs-progs: raw_btrfs and subvolume output formats'
            'dosfstools: build bootable images'
            'edk2-ovmf: run bootable images in QEMU')
provides=('mkosi')
conflicts=('mkosi')
source=("git://github.com/systemd/mkosi.git")
md5sums=('SKIP')

pkgver() {
  cd "mkosi"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "mkosi"

  python setup.py install --root="$pkgdir"
}

