# Maintainer: Carl George < arch at cgtx dot us >
# Contributor: Zohar Malamant <dotfloat gmail com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Martin Erik Werner ("arand") <martinerikwerner@gmail.com>

pkgname="redeclipse"
pkgver="1.5.5"
pkgrel="1"
pkgdesc="free, casual arena shooter"
arch=("i686" "x86_64")
url="http://redeclipse.net"
license=("ZLIB" "CCPL:by-sa" "CCPL:by" "custom")
makedepends=("ed" "sdl2_mixer" "sdl2_image" "enet")
depends=("sdl2_image" "sdl2_mixer" "enet" "hicolor-icon-theme")
source=("base-${pkgver}.tar.gz::https://github.com/red-eclipse/base/archive/v${pkgver}.tar.gz"
        "acerspyro-${pkgver}.tar.gz::https://github.com/red-eclipse/acerspyro/archive/v${pkgver}.tar.gz"
        "actors-${pkgver}.tar.gz::https://github.com/red-eclipse/actors/archive/v${pkgver}.tar.gz"
        "appleflap-${pkgver}.tar.gz::https://github.com/red-eclipse/appleflap/archive/v${pkgver}.tar.gz"
        "blendbrush-${pkgver}.tar.gz::https://github.com/red-eclipse/blendbrush/archive/v${pkgver}.tar.gz"
        "caustics-${pkgver}.tar.gz::https://github.com/red-eclipse/caustics/archive/v${pkgver}.tar.gz"
        "crosshairs-${pkgver}.tar.gz::https://github.com/red-eclipse/crosshairs/archive/v${pkgver}.tar.gz"
        "dziq-${pkgver}.tar.gz::https://github.com/red-eclipse/dziq/archive/v${pkgver}.tar.gz"
        "elyvisions-${pkgver}.tar.gz::https://github.com/red-eclipse/elyvisions/archive/v${pkgver}.tar.gz"
        "fonts-${pkgver}.tar.gz::https://github.com/red-eclipse/fonts/archive/v${pkgver}.tar.gz"
        "freezurbern-${pkgver}.tar.gz::https://github.com/red-eclipse/freezurbern/archive/v${pkgver}.tar.gz"
        "john-${pkgver}.tar.gz::https://github.com/red-eclipse/john/archive/v${pkgver}.tar.gz"
        "jojo-${pkgver}.tar.gz::https://github.com/red-eclipse/jojo/archive/v${pkgver}.tar.gz"
        "jwin-${pkgver}.tar.gz::https://github.com/red-eclipse/jwin/archive/v${pkgver}.tar.gz"
        "luckystrike-${pkgver}.tar.gz::https://github.com/red-eclipse/luckystrike/archive/v${pkgver}.tar.gz"
        "maps-${pkgver}.tar.gz::https://github.com/red-eclipse/maps/archive/v${pkgver}.tar.gz"
        "mayhem-${pkgver}.tar.gz::https://github.com/red-eclipse/mayhem/archive/v${pkgver}.tar.gz"
        "mikeplus64-${pkgver}.tar.gz::https://github.com/red-eclipse/mikeplus64/archive/v${pkgver}.tar.gz"
        "misc-${pkgver}.tar.gz::https://github.com/red-eclipse/misc/archive/v${pkgver}.tar.gz"
        "nieb-${pkgver}.tar.gz::https://github.com/red-eclipse/nieb/archive/v${pkgver}.tar.gz"
        "nobiax-${pkgver}.tar.gz::https://github.com/red-eclipse/nobiax/archive/v${pkgver}.tar.gz"
        "particles-${pkgver}.tar.gz::https://github.com/red-eclipse/particles/archive/v${pkgver}.tar.gz"
        "philipk-${pkgver}.tar.gz::https://github.com/red-eclipse/philipk/archive/v${pkgver}.tar.gz"
        "projectiles-${pkgver}.tar.gz::https://github.com/red-eclipse/projectiles/archive/v${pkgver}.tar.gz"
        "props-${pkgver}.tar.gz::https://github.com/red-eclipse/props/archive/v${pkgver}.tar.gz"
        "skyboxes-${pkgver}.tar.gz::https://github.com/red-eclipse/skyboxes/archive/v${pkgver}.tar.gz"
        "snipergoth-${pkgver}.tar.gz::https://github.com/red-eclipse/snipergoth/archive/v${pkgver}.tar.gz"
        "sounds-${pkgver}.tar.gz::https://github.com/red-eclipse/sounds/archive/v${pkgver}.tar.gz"
        "textures-${pkgver}.tar.gz::https://github.com/red-eclipse/textures/archive/v${pkgver}.tar.gz"
        "torley-${pkgver}.tar.gz::https://github.com/red-eclipse/torley/archive/v${pkgver}.tar.gz"
        "trak-${pkgver}.tar.gz::https://github.com/red-eclipse/trak/archive/v${pkgver}.tar.gz"
        "ulukai-${pkgver}.tar.gz::https://github.com/red-eclipse/ulukai/archive/v${pkgver}.tar.gz"
        "unnamed-${pkgver}.tar.gz::https://github.com/red-eclipse/unnamed/archive/v${pkgver}.tar.gz"
        "vanities-${pkgver}.tar.gz::https://github.com/red-eclipse/vanities/archive/v${pkgver}.tar.gz"
        "vegetation-${pkgver}.tar.gz::https://github.com/red-eclipse/vegetation/archive/v${pkgver}.tar.gz"
        "weapons-${pkgver}.tar.gz::https://github.com/red-eclipse/weapons/archive/v${pkgver}.tar.gz"
        "wicked-${pkgver}.tar.gz::https://github.com/red-eclipse/wicked/archive/v${pkgver}.tar.gz"
        "redeclipse-1.5.5-build-with-system-enet.patch")
sha256sums=('26799fa1def529a131f40e19a93db7897ba0c7b91ef81e6d91289a834a318376'
            'f71e7575605999bf268a85f842b26925479a40280f9c4e8fa937ab5aa229d868'
            '613b246036de98d223bd8725a5329c7d73544b474f2dde9f0d2812f323ab9449'
            '04cf453782c6aa39da25e6150b587915e23e65db047ee061a355e9d9fe8be5c7'
            '7b9dbba44d91201fcf718d110de388a155a87c0702dca6588fad887b6de9e3c3'
            '9094bf5fba0211dee091c93be876ff18d82018974653238f9bd4b04c0d914b17'
            'd0b5c2817964d2730c2e2324fa75bacd3ffa1c28e5c53f83f32da4ba0deb6e17'
            '0629a10b07b5bd0c0efb0b85ca8edef047095d2648da10651c9c2211827acb6c'
            '29b6b69a4175e37e992e71e96e03354c3ea1bd369dd2b0c9aef83d40585fed3b'
            'f97d5041083abbc09daf87c5bbe2679c3b74cdd00128197c4be4466552b37b69'
            '96e3fab4fee56b1ea2782d2a275f99c643aa88d3eb380cccc0ae0a82ff6fc04c'
            '819de2100643c9a543d735e58362a8ffc74a3c438350dff656ef81ea36dac9d2'
            '6175081cd3c6fa30d0b315b411f8dedc2fab0e2d58bb0336c5a62ea205a2af31'
            '406c51a55c4f4bb2810389bb37162157c303b83e889058a57b9379e762dee579'
            '334dd7f1a52563af4a3b0ba8a73de045262a075cd462046acadafe04b0620239'
            '94ca178eab91b9765690f45060095f72f2ab682f33c5d3a15522dc58071ca292'
            '2a03e51b536bc9da2c13e0aaebb2e7f58245814fd26122ce5327629599c9b937'
            '9a056ba8dece0bee02b6312f39767943a459093ea337d8b511adc131b38f0f10'
            '184cba7f7dd201ab94ae089251c654f6d1d8bd2dc66d41f3110fea6761d2ae1f'
            'b559547b0e84dd5f5b4022fe039113fbd0a56c07bbf162ad587b1f1d8ed40bf7'
            '06ceb535f31ce50879835b2f559072fa0cfb108c4e67b2d517d90852227c76d8'
            'eda1cba383d02865cab39bc9bc7802df2d61bfa01d1d014aefcf0daaa6b7407a'
            '926dab518e6d2c2ef88febfe830543ec6c76edd3e585d7ebb253c384fbcc73d4'
            '6ae53481528c2a116078cbd088d4b0bfcdb28a35234cda06c37f1f25a9da5c17'
            'de0287f86675084b5bd11eafec4385c04502698c719a6e80bce5135df83311b3'
            '2ce6867d109deecc967ccd2f4052e6b76e7d5a4cc9e201054589829510eda6d6'
            '94566bebdddd73d6e76ac74ef6cb78e69c7636a850484c9cdc1e30fb09d0edda'
            '5408f1b1261f6c2a88d42220eaca6593d9c6578e9d25a5a2ff1a32e0be92070f'
            'd12a69bc17736aabd58eee9fff20f3d81e1a769baa18a6a0bf3ccf9e54f758b1'
            '34eed3582080dd6229edbd0d1ede0b81c72686b651e564e5c01eb78affa4e8c2'
            'd9f7dfb34d65d39771b1712efb3abb22954abbc5843d5ace87008fd195fc8c72'
            '64b0b2263afa3fc74a573871f1854a8458527ed9a46ae2356c518be43a3e7636'
            'ab99d7ee850795781cd1b50ef8eba7883278c7580a23d83dd795de0daeab355d'
            '9b8ac66a71b3c5e3230675095acfc751a254662b6bf7c54a9f44ee4a474143f0'
            'f6b2439e236f2503d81bf95c560ed0b93215bd46d1b448e7fe4e985da1a4814b'
            '9c4f69a51e379df05e401bf5cd5319c80c74ba982d8b68ea9bf078a283cfc4dc'
            'a9f546cd75f21eabe2f0a2b7618193e2b08cc773fe6a893328d0d312acf0b7ce'
            '394cfa3f64385f05935467c88243432425304ef73dabd96fc35dcd66742f7bf9')

prepare() {
    cd "${srcdir}/base-${pkgver}"
    for i in \
    acerspyro actors appleflap blendbrush caustics crosshairs dziq elyvisions \
    fonts freezurbern john jojo jwin luckystrike maps mayhem mikeplus64 misc \
    nieb nobiax particles philipk projectiles props skyboxes snipergoth sounds \
    textures torley trak ulukai unnamed vanities vegetation weapons wicked
    do
        rmdir "data/${i}"
        mv "${srcdir}/${i}-${pkgver}" "data/${i}"
    done
    patch -p0 -i "${srcdir}/redeclipse-1.5.5-build-with-system-enet.patch"
}

build() {
    cd "${srcdir}/base-${pkgver}"
    make -C src/ client server
}

package() {
    cd "${srcdir}/base-${pkgver}"
    make -C src/ DESTDIR="${pkgdir}" prefix=/usr system-install
    install -Dm 0644 "${srcdir}/base-${pkgver}/doc/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
    install -Dm 0644 "${srcdir}/base-${pkgver}/doc/all-licenses.txt" "${pkgdir}/usr/share/licenses/${pkgname}/all-licenses.txt"
}
