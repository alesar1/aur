# Maintainer: Robert Cegli≈Ñski <rob.ceglinski@gmail.com>
# Contributor: Christopher Snowhill <chris@kode54.net>
# Contributor: Denis Zheleztsov <difrex.punk@gmail.com>

pkgname=wlr-randr-git
pkgver=0.1.0.r0.g988a802
pkgrel=2
pkgdesc="A xrandr clone for wlroots compositors (latest git version)"
arch=('x86_64')
url="https://github.com/emersion/wlr-randr"
license=('MIT')
depends=("wayland")
makedepends=("git" "meson" "ninja")
provides=("wlr-randr")
conflicts=("wlr-randr")
source=("$pkgname::git+https://github.com/emersion/wlr-randr.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
