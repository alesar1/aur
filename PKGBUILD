# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

pkgname=libopencm3-git
pkgver=r1767.ad5ec6a
pkgrel=1
pkgdesc='Open Source firmware library for various ARM Cortex microcontrollers'
arch=(any)
url='http://www.libopencm3.org/'
license=(GPL)
makedepends=(git arm-none-eabi-gcc arm-none-eabi-newlib python-yaml)
options=(!strip staticlibs !buildflags)
source=(git://github.com/libopencm3/libopencm3.git
        remove_install.patch::https://github.com/libopencm3/libopencm3/commit/7c1cd2e5cd3f3d1193ee375b46cfd8afaccb89f7.patch)
sha256sums=('SKIP'
            'd611a1dedd6b86eed552247bf13ee06343b14adfa863b9edc52039441b18120d')

pkgver() {
  cd libopencm3
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd libopencm3
  patch -p1 -R < ../remove_install.patch
}

build() {
  cd libopencm3
  make
}

package() {
  cd libopencm3
  make DESTDIR="$pkgdir" PREFIX=/usr/arm-none-eabi install
}
